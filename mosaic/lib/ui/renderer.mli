(** Renderer: layout, drawing, hit testing, and event dispatch.

    The renderer drives a three-pass pipeline over a {!Renderable.t} tree:
    - {e Lifecycle}: runs per-frame and resize hooks on registered nodes.
    - {e Layout and command generation}: computes layout via Toffee, walks the
      tree depth-first to extract absolute positions, and builds a flat render
      command list (opacity, scissors, draw calls).
    - {e Execution}: replays the command list to populate the grid and hit grid.

    After the pipeline, {!render} diffs the grid against the previous frame and
    returns minimal ANSI output.

    The renderer owns the root {!Renderable.t}, the {!Screen.t}, and all
    pipeline state. Widget code builds the tree under {!root}; the event loop
    calls {!render_frame} and {!render}, then dispatches input events. *)

(** {1:types Types} *)

type t
(** The type for renderer state. Owns the root renderable, layout tree, screen,
    and all render pipeline state. *)

(** {1:constructors Constructors} *)

val create :
  ?width_method:Matrix.Text.width_method ->
  ?clock:(unit -> float) ->
  ?screen:Matrix.Screen.t ->
  ?style:Toffee.Style.t ->
  unit ->
  t
(** [create ()] is a renderer with a root renderable and an empty screen. The
    optional parameters are:
    - [width_method]: the text width computation method. Defaults to [`Unicode].
    - [clock]: the time source for the underlying {!Matrix.Screen.t}; see
      {!Matrix.Screen.create}.
    - [screen]: an existing screen to adopt instead of creating one — pass the
      host runtime's {!Matrix.screen} so {!render_frame} builds directly into it
      and the host presents frames with a single diff. Alpha blending is enabled
      on the adopted screen's grids. [width_method] and [clock] are ignored when
      [screen] is given.
    - [style]: the root node's initial style. Defaults to
      {!Toffee.Style.default}. *)

(** {1:accessors Accessors} *)

val root : t -> Renderable.t
(** [root t] is the root renderable. Build the UI tree under this node. *)

val screen : t -> Screen.t
(** [screen t] is the underlying screen. *)

(** {1:pending_work Pending render work} *)

module Pending : sig
  type t
  (** The type for pending render work associated with the node reporting it. *)

  val node : t -> Renderable.t
  (** [node t] is the renderable reporting pending work. *)

  val work : t -> Renderable.Pending.t
  (** [work t] is the pending work description. *)
end

type settle_result = [ `Settled | `Pending of Pending.t list ]
(** The result of a bounded settlement render. *)

val pending_work : t -> Pending.t list
(** [pending_work t] is the pending render work currently reported by visible
    nodes in [t]'s tree. *)

val is_settled : t -> bool
(** [is_settled t] is [true] iff [pending_work t] is empty and no renderable has
    requested another render pass. *)

(** {1:rendering Rendering} *)

val render_frame :
  ?layout_height:int -> t -> width:int -> height:int -> delta:float -> unit
(** [render_frame t ~width ~height ~delta] builds the next frame.

    Each call replaces the complete unpresented frame. Blank cells therefore
    belong to the new pass rather than compositing over glyphs from an earlier
    pass, including when {!render_frame_until_settled} renders multiple passes
    before presentation.

    The pipeline runs in order:
    - Runs lifecycle passes ([on_frame] and resize hooks).
    - Runs frame callbacks (see {!add_frame_callback}).
    - Computes layout via Toffee.
    - Walks the tree: extracts layout and builds the render command list,
      omitting subtrees whose ancestor clip does not intersect the frame.
    - Executes render commands: draws to the grid and populates the hit grid.
    - Rechecks hover state against the frame's hit grid.

    [width] and [height] are the frame dimensions in terminal cells. [delta] is
    elapsed milliseconds since the last frame.

    [layout_height] (default [height]) is the height given to layout. Pass a
    larger value when content exceeds the presented viewport — for example a
    primary-mode host growing its live region — so layout computes against the
    content height while the screen keeps the viewport size; rows below [height]
    are clipped. *)

val render : ?full:bool -> t -> string
(** [render t] diffs the current frame against the previous one and returns the
    minimal ANSI output string. Call after {!render_frame}.

    When [full] is [true], all cells are emitted regardless of changes. [full]
    defaults to [false]. Frame timestamps come from the [clock] given to
    {!create}.

    Do not call this on a renderer that adopted a host screen — presentation
    belongs to the host runtime there. *)

val needs_render : t -> bool
(** [needs_render t] is [true] iff a renderable has requested a re-render or
    live nodes are active. *)

val render_frame_until_settled :
  ?max_passes:int ->
  t ->
  width:int ->
  height:int ->
  delta:float ->
  settle_result
(** [render_frame_until_settled t ~width ~height ~delta] renders bounded frame
    passes until visible nodes report no pending render work and no renderable
    requests a follow-up pass.

    [max_passes] defaults to [4]. The function does not block for external
    asynchronous work; if work remains after the pass budget, it returns
    [`Pending pending]. [pending] may be empty when the remaining work is an
    undescribed render request rather than a pending-work provider. *)

(** {1:events Event dispatch} *)

val dispatch_key : t -> Input.Key.event -> Event.key
(** [dispatch_key t key] sends [key] to the focused renderable and returns the
    resulting event.

    If the focused node does not prevent default, the default key handler runs.
    The returned event carries the [default_prevented] flag set by the focused
    node's handler; callers can inspect it to determine whether the key was
    consumed. *)

val dispatch_mouse : t -> Input.Mouse.event -> unit
(** [dispatch_mouse t mouse] runs the full mouse dispatch pipeline:
    - Updates pointer state.
    - Hit-tests the mouse position. Wheel events that hit nothing are retargeted
      at the focused renderable, so a scroll over dead space still reaches the
      nearest scrollable ancestor.
    - Advances the selection state machine (start, update, or finish).
    - Tracks hover state and fires [Over]/[Out] events on target change.
    - Redirects events other than wheel events to the drag-captured node when
      active.
    - Dispatches with bubbling to the hit-tested node.
    - Auto-focuses on left click.
    - Clears stale selection if not prevented. *)

val dispatch_paste : t -> string -> unit
(** [dispatch_paste t text] sends [text] as a paste event to the focused
    renderable. *)

(** {1:focus Focus} *)

val focused : t -> Renderable.t option
(** [focused t] is the currently focused renderable, if any. *)

val focus : t -> Renderable.t -> bool
(** [focus t node] focuses [node] and is [true] iff [node] is focusable. *)

val blur : t -> unit
(** [blur t] removes focus from the currently focused renderable. *)

(** {1:selection Selection} *)

val selection : t -> Selection.t option
(** [selection t] is the active text selection, if any. *)

val selection_text : t -> string option
(** [selection_text t] is the text of the active selection, gathered in document
    order from the selectables under its container, or [None] when no selection
    is active or the selection is empty. *)

val clear_selection : t -> unit
(** [clear_selection t] clears the active text selection, notifying all
    selectable renderables under the selection container. *)

(** {1:drag Drag capture} *)

val captured : t -> Renderable.t option
(** [captured t] is the renderable currently capturing all mouse events during a
    drag gesture, if any. *)

(** {1:hover Hover} *)

val hover : t -> Renderable.t option
(** [hover t] is the renderable currently under the mouse pointer, if any. *)

(** {1:frame_callbacks Frame callbacks} *)

val add_frame_callback : t -> (float -> unit) -> unit
(** [add_frame_callback t f] registers [f] to run at the start of each frame
    with delta time in milliseconds. Callbacks run after lifecycle passes and
    before layout computation.

    See also {!remove_frame_callback} and {!clear_frame_callbacks}. *)

val remove_frame_callback : t -> (float -> unit) -> unit
(** [remove_frame_callback t f] unregisters [f] using physical equality.

    See also {!add_frame_callback}. *)

val clear_frame_callbacks : t -> unit
(** [clear_frame_callbacks t] removes all registered frame callbacks.

    See also {!add_frame_callback}. *)

(** {1:post_process Post-processing} *)

val add_post_process : t -> (Grid.t -> delta:float -> unit) -> Screen.effect_id
(** [add_post_process t f] registers [f] as a persistent post-processing
    transform on the underlying screen. [f] receives the rendered {!Grid.t} and
    the frame delta in milliseconds; it runs after frame building and before
    diffing. Returns a {!Screen.effect_id} for later removal.

    See also {!remove_post_process} and {!clear_post_processes}. *)

val remove_post_process : t -> Screen.effect_id -> unit
(** [remove_post_process t id] unregisters the post-processor identified by
    [id].

    See also {!add_post_process}. *)

val clear_post_processes : t -> unit
(** [clear_post_processes t] removes all registered post-processing functions.

    See also {!add_post_process}. *)
